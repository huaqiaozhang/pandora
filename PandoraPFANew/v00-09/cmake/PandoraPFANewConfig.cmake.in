##############################################################################
# cmake configuration file for PandoraPFANew
#
# requires:
#   MacroCheckPackageLibs.cmake for checking package libraries
#
# returns following variables:
#
#   PandoraPFANew_FOUND      : set to TRUE if PandoraPFANew found
#       if FIND_PACKAGE called with REQUIRED and COMPONENTS arguments
#       PandoraPFANew_FOUND is only set to TRUE if ALL components are also found
#       if REQUIRED is NOT set components may or may not be available
#
#   PandoraPFANew_ROOT       : path to this PandoraPFANew installation
#   PandoraPFANew_VERSION    : package version
#   PandoraPFANew_LIBRARIES  : list of PandoraPFANew libraries (NOT including COMPONENTS)
#   PandoraPFANew_INCLUDE_DIRS  : list of paths to be used with INCLUDE_DIRECTORIES
#   PandoraPFANew_LIBRARY_DIRS  : list of paths to be used with LINK_DIRECTORIES
#   PandoraPFANew_COMPONENT_LIBRARIES      : list of PandoraPFANew component libraries
#   PandoraPFANew_${COMPONENT}_FOUND       : set to TRUE or FALSE for each library
#   PandoraPFANew_${COMPONENT}_LIBRARY     : path to individual libraries
#   PandoraPFANew_${COMPONENT}_LIB_DEPENDS : individual library dependencies
#
# @author Jan Engels, Desy
##############################################################################

SET( PandoraPFANew_ROOT "@CMAKE_INSTALL_PREFIX@" )
SET( PandoraPFANew_VERSION "@PandoraPFANew_VERSION@" )


# ---------- include dirs -----------------------------------------------------
# do not store find results in cache
SET( PandoraPFANew_INCLUDE_DIRS PandoraPFANew_INCLUDE_DIRS-NOTFOUND )
MARK_AS_ADVANCED( PandoraPFANew_INCLUDE_DIRS )

FIND_PATH( PandoraPFANew_INCLUDE_DIRS
	NAMES Pandora/Pandora.h
	PATHS ${PandoraPFANew_ROOT}/include
	NO_DEFAULT_PATH
)



# ---------- libraries --------------------------------------------------------
INCLUDE( "@ILCSOFT_CMAKE_MODULES_ROOT@/MacroCheckPackageLibs.cmake" )

# only standard libraries should be passed as arguments to CHECK_PACKAGE_LIBS
# additional components are set by cmake in variable PKG_FIND_COMPONENTS
# first argument should be the package name
CHECK_PACKAGE_LIBS( PandoraPFANew PandoraFramework PandoraFineGranularityContent PandoraKMeansContent )




# ---------- libraries dependencies -------------------------------------------
# this sets PandoraPFANew_${COMPONENT}_LIB_DEPENDS variables
INCLUDE( "${PandoraPFANew_ROOT}/lib/cmake/PandoraPFANewLibDeps.cmake" )
 



# ---------- final checking ---------------------------------------------------
INCLUDE( FindPackageHandleStandardArgs )
# set PANDORAPFANEW_FOUND to TRUE if all listed variables are TRUE and not empty
# PandoraPFANew_COMPONENT_VARIABLES will be set if FIND_PACKAGE is called with REQUIRED argument
FIND_PACKAGE_HANDLE_STANDARD_ARGS( PandoraPFANew DEFAULT_MSG PandoraPFANew_ROOT PandoraPFANew_INCLUDE_DIRS PandoraPFANew_LIBRARIES ${PandoraPFANew_COMPONENT_VARIABLES} )

SET( PandoraPFANew_FOUND ${PANDORAPFANEW_FOUND} )

